
[[_project_structure]]
= Project Structure

It's expected of projects that make use of these plugins to follow a certain directory structure that enables them to gain
higher benefits via conventions rather than writing lots of custom configuration elements.

To begin with, projects should have a multi-project nature, i.e, be comprised of a root project and at least 1 subproject
or module. Why is this? This way a root project serves as the central point of configuration, while additional subprojects
may be added at later stages during development. Thus the minimum structure could look like this

[source,groovy]
----
.
├── build.gradle
├── gradle
│   └── wrapper
│       ├── gradle-wrapper.jar
│       └── gradle-wrapper.properties
├── gradle.properties
├── gradlew
├── gradlew.bat
├── settings.gradle
└── subprojects
    └── project1
        ├── build.gradle
        └── src
            └── main
----

There are many ways to structure a multi-project build. Some prefer adding all submodule directories at the root level,
others prefer a two or more levels structure. None of the plugins enforce a particular directory structure, you're free
to pick the structure that suits your style better. Personally I prefer the following structure

[source,groovy]
----
.
├── build.gradle
├── gradle
│   └── wrapper
│       ├── gradle-wrapper.jar
│       └── gradle-wrapper.properties
├── gradle.properties
├── gradlew
├── gradlew.bat
├── settings.gradle
├── docs
│   └── guide
│       ├── guide.gradle
│       └── src
│           └── docs
│               ├── asciidoc
│               └── resources
└── subprojects
    ├── project1
    │   ├── project1.gradle
    │   └── src
    │       └── main
    └── project2
        ├── project2.gradle
        └── src
            └── main
----

You'll notice that source code projects are placed under `subprojects` while documentation projects are placed under `docs`.
Also, the names of the build files have been changed to reflect the name of the project they belong to. This change must
be reflected in the `settings.gradle` (Groovy) or `settings.gradle.kts` (Kotlin) file, using code similar to

[source,groovy,indent=0,subs="verbatim,attributes",role="primary"]
.Groovy
----
rootProject.name = 'root'

def includeProject = { String projectDirName, String projectName ->
    File baseDir = new File(settingsDir, projectDirName)
    File projectDir = new File(baseDir, projectName)
    String buildFileName = "${projectName}.gradle"

    assert projectDir.isDirectory()
    assert new File(projectDir, buildFileName).isFile()

    include projectName
    project(":${projectName}").projectDir    = projectDir
    project(":${projectName}").buildFileName = buildFileName
}

['docs', 'subprojects'].each { dirName ->
    File subdir = new File(rootDir, dirName)
    subdir.eachDir { dir ->
        File buildFile = new File(dir, "${dir.name}.gradle")
        if (buildFile.exists()) {
            includeProject dirName, dir.name
        }
    }
}
----

[source,kotlin,indent=0,subs="verbatim,attributes",role="secondary"]
.Kotlin
----
rootProject.name = "root"

fun includeProject(projectDirName: String, projectName: String) {
    val baseDir = File(settingsDir, projectDirName)
    val projectDir = File(baseDir, projectName)
    val buildFileName = "${projectName}.gradle.kts"

    assert(projectDir.isDirectory())
    assert(File(projectDir, buildFileName).isFile())

    include(projectName)
    project(":${projectName}").projectDir    = projectDir
    project(":${projectName}").buildFileName = buildFileName
}

listOf("docs", "subprojects").forEach { dirName ->
    val subdir = File(rootDir, dirName)
    subdir.walkTopDown().maxDepth(1).forEach { dir ->
        val buildFile = File(dir, "${dir.name}.gradle.kts")
        if (buildFile.exists()) {
            includeProject(dirName, dir.name)
        }
    }
}
----

Alternatively, you may apply the `<<_org_kordamp_gradle_settings,org.kordamp.gradle.settings>>` plugin to your
`settings.gradle(.kts)` file to obtain the same behavior:

[source,groovy,indent=0,subs="verbatim,attributes",role="primary"]
.settings.gradle
----
buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'org.kordamp.gradle:settings-gradle-plugin:{project-version}'
    }
}
apply plugin: 'org.kordamp.gradle.settings'

rootProject.name = 'root'

projects {
    directories = ['docs', 'subprojects']
}
----

[source,kotlin,indent=0,subs="verbatim,attributes",role="secondary"]
.settings.gradle.kts
----
buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath("org.kordamp.gradle:settings-gradle-plugin:{project-version}")
    }
}
apply(plugin = "org.kordamp.gradle.settings")

rootProject.name = "root"

configure<org.kordamp.gradle.plugin.settings.ProjectsExtension> {
    directories = listOf("docs", "subprojects")
}
----

With this structure in place the next step would be to setup the minimum configuration on the root project's build file

[source,groovy,indent=0,subs="verbatim,attributes",role="primary"]
.Groovy
----
plugins {
    id 'org.kordamp.gradle.java-project' version '{project-version}'      //<1>
    id 'org.kordamp.gradle.bintray'      version '{project-version}'      //<1>
}

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()  // <2>

    info {                                                                // <3>
        name        = 'Sample'
        vendor      = 'Acme'
        description = 'Sample project'

        links {
            website      = 'https://github.com/joecool/sample'
            issueTracker = 'https://github.com/joecool/sample/issues'
            scm          = 'https://github.com/joecool/sample.git'
        }

        people {
            person {
                id    = 'joecool'
                name  = 'Joe Cool'
                roles = ['developer']
            }
        }
    }

    licensing {                                                           // <4>
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    bintray {                                                             // <5>
        credentials {
            username = project.bintrayUsername
            password = project.bintrayApiKey
        }
        userOrg    = 'joecool'
        name       = rootProject.name
        githubRepo = 'joecool/sample'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}
----
<1> Download and apply plugins to root project
<2> Conditionally enable some features related to publishing
<3> General information for all projects
<4> License details
<5> Publishing specific information

[source,kotlin,indent=0,subs="verbatim,attributes",role="secondary"]
.Kotlin
----
plugins {
    id("org.kordamp.gradle.java-project") version "{project-version}"     //<1>
    id("org.kordamp.gradle.bintray")      version "{project-version}"     //<1>
}

val bintrayUsername: String by project
val bintrayApiKey: String by project

config {
    release = rootProject.findProperty("release").toString().toBoolean()  //<2>

    info {                                                                //<3>
        name        = "Sample"
        vendor      = "Acme"
        description = "Sample project"

        links {
            website      = "https://github.com/joecool/sample"
            issueTracker = "https://github.com/joecool/sample/issues"
            scm          = "https://github.com/joecool/sample.git"
        }

        people {
            person {
                id    = "joecool"
                name  = "Joe Cool"
                roles = listOf("developer")
            }
        }
    }

    licensing {                                                           //<4>
        licenses {
            license {
                id = org.kordamp.gradle.plugin.base.model.LicenseId.APACHE_2_0
            }
        }
    }

    bintray {                                                             // <5>
        credentials {
            username = project.bintrayUsername
            password = project.bintrayApiKey
        }
        userOrg    = "joecool"
        name       = rootProject.name
        githubRepo = "joecool/sample"
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}
----
<1> Download and apply plugins to root project
<2> Conditionally enable some features related to publishing
<3> General information for all projects
<4> License details
<5> Publishing specific information

This minimal configuration enables the following features:

 * Generate additional JAR manifest entries if [conum,data-value=2]_2_ is enabled.
 * Generate a `-sources` JAR file with all sources per project.
 * Configure the `javadoc` task for each project using information found in the config block, such as author, copyright
   year, default settings.
 * Generate a `-javadoc` JAR file with the output of the `javadoc` task, per project.
 * Generate a task on the root project that collects all Javadocs.
 * Generate a task on the root project that packages the aggregated javadocs.
 * Configure the license plugin with the license details [conum,data-value=4]_4_.
 * Configure the `maven-publish` plugin with data defined in [conum,data-value=3]_3_ and [conum,data-value=4]_4_. The
  `-sources` and  `-javadoc` JARs are automatically added to the default publication.
 * Configure the `bintray` plugin with data defined in [conum,data-value=5]_5_.
 * Configure the `jacoco` plugin on each project.
 * Configure aggregate JaCoCo reports on the root project.
 * Generate a source stats task per project.
 * Generate a task at the root project to collect aggregate source stats.
 * Generates a task per project that creates pretty-printed sources (HTML).
 * Generate a task at the root project to collect pretty-printed sources.

The build file for the `guide` project can be as simple as

[source,groovy,indent=0,subs="verbatim,attributes",role="primary"]
.Groovy
----
apply plugin: 'org.kordamp.gradle.guide'
----

[source,kotlin,indent=0,subs="verbatim,attributes",role="secondary"]
.Kotlin
----
apply(plugin = "org.kordamp.gradle.guide")
----

And the build file for each subproject may look like

[source,groovy,indent=0,subs="verbatim,attributes",role="primary"]
.Groovy
----
apply plugin: 'java'

dependencies {
    // dependencies
}
----

[source,kotlin,indent=0,subs="verbatim,attributes",role="secondary"]
.Kotlin
----
apply(plugin = "java")

dependencies {
    // dependencies
}
----

There are 4 language specific project aggregators and 1 generic project aggregator

[options="header"]
|===
| Type    | Plugin
| Generic | `<<_org_kordamp_gradle_project,org.kordamp.gradle.project>>`
| Java    | `<<_org_kordamp_gradle_java_project,org.kordamp.gradle.java-project>>`
| Groovy  | `<<_org_kordamp_gradle_groovy_project,org.kordamp.gradle.groovy-project>>`
| Kotlin  | `<<_org_kordamp_gradle_kotlin_project,org.kordamp.gradle.kotlin-project>>`
| Scala   | `<<_org_kordamp_gradle_scala_project,org.kordamp.gradle.scala-project>>`
|===

